!
!
!===============================================================================
! module interface to FESOM2.0 for the CVMIX TIDAL extension for the calculation 
! tidal induces vertical mixing after Simmons et al. 2004
module g_cvmix_tidal
    
    !___________________________________________________________________________
    ! module calls from cvmix library
    use cvmix_tidal,  only :  cvmix_init_tidal, cvmix_coeffs_tidal_low, cvmix_compute_Simmons_invariant_low                 
    use cvmix_kinds_and_types, only: cvmix_data_type, cvmix_global_params_type
    
    !___________________________________________________________________________
    ! module calls from FESOM
    use g_config , only: dt
    use o_param           
    use mod_mesh
    USE MOD_PARTIT
    USE MOD_PARSUP
    use o_arrays
    use g_comm 
    use g_read_other_NetCDF
    implicit none
    public
    
    !___Parameter for the init of CVMIX TIDAL___________________________________
    ! Parameters associated with the Simmons et al. scheme
    character(20) :: tidal_mixscheme="Simmons"
    
    ! units: unitless (fraction); (Gamma in Simmons et al.), mixing efficiency, 
    ! take to be 0.2 (Osborn,1980)
    real(kind=WP) :: tidal_efficiency=0.2                   
    
    ! units: m; zeta in Simmons et al. (used to compute the vertical deposition 
    ! function), i.e. mixing from breaking internal gravity waves, generated by 
    ! scattered barotropic tidal energy, is exponentially trapped within a 
    ! distance 'zeta' from the bottom.
    real(kind=WP) :: tidal_vert_decayscale=500.0       
    
    ! units: m^2/s; largest acceptable value for diffusivity
    real(kind=WP) :: tidal_max_coeff=50e-4            
    
    ! units: unitless (fraction);  tidal dissipation efficiency (q in Simmons 
    ! et al.), i.e. fraction of energy that dissipates locally
    ! Physical arguments suggest that 60–90% of this baroclinic wave energy is 
    ! contained in low-mode internal waves that are able to propagate large distances 
    ! from the generation site. The remaining portion of the energy lost from the 
    ! barotropic tide, denoted the ‘‘tidal dissipation efficiency’’ (q), dissipates 
    ! as locally enhanced turbulent mixing. St. Laurent et al. (2002) assumed that 
    ! q = 1/3 of the generated energy flux is dissipated locally, with the remaining 
    ! 1 - q = 2/3 radiating away as low mode internal waves.
    real(kind=WP) :: tidal_lcl_mixfrac=0.33           
    
    ! units: m; depth of the shallowest column where tidal mixing is 
    ! computed (positive below surface)
    real(kind=WP) :: tidal_depth_cutoff=0.0              

    ! filelocation for idemix bottom forcing 
    character(MAX_PATH):: tidal_botforc_file = '/work/ollie/pscholz/FORCING/IDEMIX/tidal_energy_gx1v6_20090205_rgrid.nc'
    character(MAX_PATH):: tidal_botforc_vname= 'wave_dissipation'
    real(kind=WP)      :: tidal_botforc_Etot = 0.0_WP ! units W
    
    namelist /param_tidal/ tidal_mixscheme, tidal_efficiency, tidal_vert_decayscale, &
                           tidal_max_coeff, tidal_lcl_mixfrac,tidal_depth_cutoff,    &
                           tidal_botforc_file, tidal_botforc_vname, tidal_botforc_Etot
    
    type(cvmix_global_params_type) :: CVmix_tidal_params ! reads the 'Prandtl' number in
    
    !___________________________________________________________________________
    ! CVMIX-TIDAL variables
    real(kind=WP), allocatable, dimension(:,:) :: tidal_Av
    real(kind=WP), allocatable, dimension(:,:) :: tidal_Kv
    real(kind=WP), allocatable, dimension(:)   :: tidal_fbot
    
    contains
    !
    !
    !
    !===========================================================================
    ! allocate and initialize IDEMIX variables --> call initialisation 
    ! routine from cvmix library
    subroutine init_cvmix_tidal(partit, mesh)
        
        character(len=MAX_PATH)               :: nmlfile
        logical                               :: file_exist=.False.
        integer                               :: node_size, elem_size, elem
        real(kind=WP)                         :: loc_Etot=0.0_WP, glb_Etot=0.0_WP
        type(t_mesh),   intent(in),    target :: mesh
        type(t_partit), intent(inout), target :: partit
        integer fileunit

#include "associate_part_def.h"
#include "associate_mesh_def.h"
#include "associate_part_ass.h"
#include "associate_mesh_ass.h" 
        !_______________________________________________________________________
        if(mype==0) then
            write(*,*) '____________________________________________________________'
            write(*,*) ' --> initialise cvmix_TIDAL'
            write(*,*)
        end if
            
        !_______________________________________________________________________
        ! allocate + initialse idemix arrays --> with size myDim_nod2D+eDim_nod2D
        node_size=myDim_nod2D+eDim_nod2D
        elem_size=myDim_elem2D+eDim_elem2D
        allocate(tidal_Av(nl,elem_size))
        allocate(tidal_Kv(nl,elem_size))
        tidal_Av(:,:)     = 0.0_WP
        tidal_Kv(:,:)     = 0.0_WP
        
        allocate(tidal_fbot(elem_size))
        tidal_fbot(:) = 0.0_WP
        
        !_______________________________________________________________________
        ! read cvmix namelist file 
        nmlfile ='namelist.cvmix'    ! name of ocean namelist file
        ! check if cvmix namelist file exists if not use default values 
        file_exist=.False.
        inquire(file=trim(nmlfile),exist=file_exist) 
        if (file_exist) then
            open(newunit=fileunit,file=trim(nmlfile))
                read(fileunit,nml=param_tidal)
            close(fileunit)
        else
            write(*,*) '     could not find namelist.cvmix, will use default values !'    
        end if    
        
        !_______________________________________________________________________
        if (mype==0) then
            write(*,*) "     tidal_mixscheme       = ", tidal_mixscheme
            write(*,*) "     tidal_efficiency      = ", tidal_efficiency
            write(*,*) "     tidal_vert_decayscale = ", tidal_vert_decayscale
            write(*,*) "     tidal_max_coeff       = ", tidal_max_coeff
            write(*,*) "     tidal_lcl_mixfrac     = ", tidal_lcl_mixfrac
            write(*,*) "     tidal_depth_cutoff    = ", tidal_depth_cutoff
            write(*,*) "     tidal_botforc_file    = ", tidal_botforc_file
            write(*,*) "     tidal_botforc_vname   = ", tidal_botforc_vname
            write(*,*) "     tidal_botforc_Etot    = ", tidal_botforc_Etot
            write(*,*)
        end if
        
        !_______________________________________________________________________
        ! read bottom near tidal forcing from cesm data set --> file 
        ! from N. Brüggemann interpoalted to regular grid
        file_exist=.False.
        inquire(file=trim(tidal_botforc_file),exist=file_exist) 
        if (file_exist) then
            if (mype==0) write(*,*) ' --> read TIDAL near tidal bottom forcing'
            call read_other_NetCDF(tidal_botforc_file, tidal_botforc_vname, 1, tidal_fbot, .true., .false.,  partit, mesh) 
            !                                                                                                  |           
            !                                 .false.=interpolate on element centroids instead of vertices <---+   
            
            ! check for total tidal energy that is infused through the bottom, see how 
            ! much is lossed during interpolation and compare with value of the 
            ! original files
            loc_Etot = 0.0_WP
            do elem=1, myDim_elem2D
                ! REMEMBER!!!: the partition on elements is not unique there are 
                ! elements that belong to two CPUs. For unique elements the index
                ! of the First trinagle node must  be <= myDim_nod2D
                if (elem2D_nodes(1,elem)<=myDim_nod2D) then
                    loc_Etot = loc_Etot + elem_area(elem)*tidal_fbot(elem)
                end if     
            end do
            call MPI_AllREDUCE(loc_Etot, glb_Etot, 1, MPI_DOUBLE_PRECISION, MPI_SUM, MPI_COMM_FESOM, MPIerr)
            if (mype==0) write(*,*) " --> TIDAL total tidal energy Etot_bot =", glb_Etot*1.0e-12, ' TW'
            
            ! normalize total tidal energy at bottom with respect to the total 
            ! tidal energy that is e.g in the original forcing files to accomodate 
            ! non concerving losses during interpolation. This is only done when 
            ! in namelist.cvmix: tidal_botforc_Etot \= 0.0_WP
            if (tidal_botforc_Etot /= 0.0_WP) then
                tidal_fbot = tidal_fbot * tidal_botforc_Etot/glb_Etot
                
                loc_Etot = 0.0_WP
                do elem=1, myDim_elem2D
                    ! REMEMBER!!!: the partition on elements is not unique there are 
                    ! elements that belong to two CPUs. For unique elements the index
                    ! of the First trinagle node must  be <= myDim_nod2D
                    if (elem2D_nodes(1,elem)<=myDim_nod2D) then
                        loc_Etot = loc_Etot + elem_area(elem)*tidal_fbot(elem)
                    end if     
                end do
                call MPI_AllREDUCE(loc_Etot, glb_Etot, 1, MPI_DOUBLE_PRECISION, MPI_SUM, MPI_COMM_FESOM, MPIerr)
                if (mype==0) write(*,*) " --> TIDAL Etot_bot after normalizing =", glb_Etot*1.0e-12, ' TW'
            end if 
            
            ! convert from W/m^2 to m^3/s^3
            ! tidal_fbot  = tidal_fbot/density_0
            ! --> the tidal energy for dissipation is divided by rho0 in 
            !     cvmix_tidal.f90 (subroutine 'cvmix_compute_Simmons_invariant_low')
            
        else
            if (mype==0) then
                write(*,*) '____________________________________________________________________'
                write(*,*) ' ERROR: TIDAL bottom forcing file not found! Cant apply TIDAL'
                write(*,*) '        vertical mixing parameterisation! '
                write(*,*) '        --> check your namelist.cvmix, tidal_botforc_file &  '
                write(*,*) '____________________________________________________________________'
            end if 
            call par_ex(partit%MPI_COMM_FESOM, partit%mype, 0)
        end if 
        
        !_______________________________________________________________________
        ! initialise TIDAL parameters
        call cvmix_init_tidal(mix_scheme           = tidal_mixscheme,        &
                              efficiency           = tidal_efficiency,       &
                              vertical_decay_scale = tidal_vert_decayscale,  &
                              max_coefficient      = tidal_max_coeff,        &
                              local_mixing_frac    = tidal_lcl_mixfrac,      &
                              depth_cutoff         = tidal_depth_cutoff)
        
    end subroutine init_cvmix_tidal
    !
    !
    !
    !===========================================================================
    ! calculate TIDAL mixing parameterisation
    subroutine calc_cvmix_tidal(partit, mesh)
        type(t_mesh),   intent(in),    target :: mesh
        type(t_partit), intent(inout), target :: partit
        integer                               :: node, elem, node_size, elem_size
        integer                               :: nz, nln, uln, nle, ule, ki
        integer                               :: elnodes(3)
        real(kind=WP)                         :: simmonscoeff, vertdep(mesh%nl)
        real(kind=WP)                         :: zbar_e(mesh%nl), Z_e(mesh%nl-1), bvfreq2(mesh%nl)
        real(kind=WP)                         :: tsum1, tvol
#include "associate_part_def.h"
#include "associate_mesh_def.h"
#include "associate_part_ass.h"
#include "associate_mesh_ass.h" 
        node_size = myDim_nod2D
        elem_size = myDim_elem2D
        
        !_______________________________________________________________________
        do elem = 1,elem_size
            nln = nlevels(elem)-1
            uln = ulevels(elem)
            vertdep = 0.0_WP
            
            !___________________________________________________________________
            ! compute full zbar_e and mid depth levels z_e at elements
            zbar_e        = 0.0_WP
            Z_e           = 0.0_WP
            zbar_e(nln+1) = zbar_e_bot(elem)
            Z_e(nln)      = zbar_e(nln+1) + helem(nln ,elem)/2.0_WP
            do nz=nln, uln+1, -1
                zbar_e(nz)= zbar_e(nz+1 ) + helem(nz  ,elem)
                Z_e(nz-1) = zbar_e(nz   ) + helem(nz-1,elem)/2.0_WP
            end do
            zbar_e(uln)   = zbar_e(uln+1) + helem(uln ,elem)
            
            !___________________________________________________________________
            ! calculate for TKE square of Brünt-Väisälä frequency, be aware that
            ! bvfreq contains already the squared brünt Väisälä frequency ...
            bvfreq2          = 0.0_WP
            do nz= uln, nln 
                elnodes      = elem2d_nodes(:,elem)
                bvfreq2(nz)  = sum(bvfreq(nz,elnodes))/3.0_WP
            end do
            
            !___________________________________________________________________
            ! Compute the time-invariant portion of the tidal mixing coefficient
            ! using the Simmons et al.(2004) scheme.
            call cvmix_compute_Simmons_invariant_low(         &
                 nlev            = nln-uln+1 ,                &
                 energy_flux     = tidal_fbot(elem),          & !in W m-2  
                 rho             = density_0,                 &
                 SimmonsCoeff    = simmonscoeff,              &
                 VertDep         = vertdep(uln:nln),          & ! vertical deposition function 
                 zw              = zbar_e(uln:nln+1),         & 
                 zt              = Z_e(uln:nln))
                
            !___________________________________________________________________
            ! Computes vertical diffusion coefficients for tidal mixing 
            ! parameterizations.
            call cvmix_coeffs_tidal_low(                      &
                 Mdiff_out       = tidal_Av(uln:nln,elem),    &
                 Tdiff_out       = tidal_Kv(uln:nln,elem),    &
                 Nsqr            = bvfreq2(uln:nln),          & !FIXME: limit to N2 > 10^-8 ? as in Simmons et al.
                 OceanDepth      = -zbar_e_bot(elem),         & !FIXME: neglecting free surface contribution
                 SimmonsCoeff    = simmonscoeff,              &
                 vert_dep        = vertdep(uln:nln),          &
                 nlev            = nln-uln+1,                       &
                 max_nlev        = nl-1,                      &
                 CVmix_params    = CVmix_tidal_params) ! FIXME: Simmons et al. use Prandtl=10.0 (atm its 1.0)
                 
!!PS             if (mype==0) then 
!!PS                 write(*,*) 'SimmonsCoeff = ',simmonscoeff
!!PS                 write(*,*) 'tidalforcbot = ',tidal_fbot(node)
!!PS                 write(*,*) 'VertDep      = ',vertdep
!!PS                 write(*,*) 'bvfreq       = ',bvfreq(:,node)
!!PS                 write(*,*) 'tidal_Kv     = ',tidal_Kv(1:nln+1,node)
!!PS                 write(*,*) 'tidal_Av     = ',tidal_Av(1:nln+1,node)
!!PS             end if 
                tidal_Av(uln,elem)   = 0.0_WP
                tidal_Kv(uln,elem)   = 0.0_WP
                tidal_Av(nln+1,elem) = 0.0_WP
                tidal_Kv(nln+1,elem) = 0.0_WP
        end do !-->do elem = 1,elem_size
            
        !_______________________________________________________________________
        ! add tidal diffusivity to main model diffusivity Kv
        ! MPIOM note 1: simplification, other option is to explicitly calculate 
        !               the tidal scheme only below the mixed layer (as a third 
        !               option in the KPP scheme for the scheme below the mixed 
        !               layer, and then set 'depth_cutoff' to the depth level of 
        !               the bottom of the mixed layer
        ! 
        ! MPIOM note 2: background diffusivities were already added in the mixed layer 
        !               scheme (KPP)
        
        !_______________________________________________________________________
        ! write out tidal diffusivity tidal_Kv --> convert from elem to vertices 
        ! --> add it to main diffusivity Kv
        do node=1, node_size 
            uln = ulevels_nod2D(node)+1
            nln = nlevels_nod2D(node)-1
            do nz=uln, nln
                tvol =0.0_WP
                tsum1=0.0_WP
                do ki=1, nod_in_elem2D_num(node)
                    elem = nod_in_elem2D(ki,node)
                    ule  = ulevels(elem)+1
                    nle  = nlevels(elem)-1
                    if (nle<nz .or. nz<ule) cycle
                    tvol = tvol  +                   elem_area(elem)
                    tsum1= tsum1 + tidal_Kv(nz,elem)*elem_area(elem)
                end do
                Kv(nz,node) = Kv(nz,node) + tsum1/tvol
            end do
        end do
        call exchange_nod(Kv, partit)
            
        !_______________________________________________________________________
        ! add tidal viscosity tidal_Av to main model viscosity Av
        Av = Av + tidal_Av
        
    end subroutine calc_cvmix_tidal
end module g_cvmix_tidal
